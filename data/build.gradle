plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.squareup.sqldelight'
    id 'org.jetbrains.kotlin.kapt'
    id 'com.google.protobuf'
}

android {
    compileSdk projectTargetSdk

    defaultConfig {
        minSdk projectMinSdk
        targetSdk projectTargetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.19.1" // TODO find how to use versions catalog
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation project(':domain')

    implementation libs.kotlin.coroutines.android
    implementation libs.sqldelight.androidDriver
    implementation libs.sqldelight.coroutines

    implementation libs.androidx.datastore
    implementation libs.protobuf.javalite

    implementation libs.hilt.android
    kapt libs.hilt.compiler

    testImplementation libs.junit
    testImplementation libs.mockk
    testImplementation libs.kotest.assertions
    testImplementation libs.sqldelight.driver
}